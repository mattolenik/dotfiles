# TODO: sort this file, use plugin structure
for f in ~/.zsh/plugins/*/*.plugin.zsh; do source "$f"; done
autoload -U add-zsh-hook
[[ $COLORTERM = *(24bit|truecolor)* ]] || zmodload zsh/nearcolor

bindkey -v  # vi keybindings, starts in insert mode

export EDITOR="nvim"

HOMEBREW_PREFIX="/opt/homebrew"

log() {
  [[ -z $LOG ]] && return
  print -P "$*\n" 1>&2
}

warn() {
  print -P "$(pcolor WARNING: yellow none bold) $*"
}

color() {
  print -nP "$(pcolor $@)"
}

acc() {
  print -n "$(pcolor "$*" green none)"
}

acc2() {
  print -n "$(pcolor "$*" blue none)"
}

cau() {
  print -n "$(pcolor "$*" yellow none)"
}

faint() {
  print -n "$(pcolor "$*" 8 none)"
}

faintacc() {
  print -n "$(pcolor "$*" 5 none)"
}

vfaint() {
  print -n "$(pcolor "$*" '#333333' none)"
}

pcolor() {
  local str=$1
  local fg=$2
  local bg=$3
  shift 3
  local attrs=$@
  local prefix="%F{$fg}"
  local suffix="%f"
  if [[ $bg != clear && $bg != none ]]; then
    prefix="$prefix%K{$bg}"
    suffix="%k$suffix"
  fi
  if (( attrs[(Ie)bold] )); then
    prefix="$prefix%B"
    suffix="%b$suffix"
  fi
  if (( attrs[(Ie)ul] )); then
    prefix="$prefix%U"
    suffix="%u$suffix"
  fi
  if (( attrs[(Ie)hl] )); then
    prefix="$prefix%S"
    suffix="%s$suffix"
  fi
  if (( attrs[(Ie)italic] )); then
    prefix="$prefix"'\e[3m'
    suffix='\e[0m'"$suffix"
  fi
  if (( attrs[(Ie)st] )); then
    prefix="$prefix"'\e[9m'
    suffix='\e[0m'"$suffix"
  fi
  print -n "%{$prefix%}$str%{$suffix%}"
}

command_exists() {
  command -v "$@" &> /dev/null
}

source_if_exists() {
  for file in "$@"; do
    if [[ -f $1 ]]; then
      log "sourcing file: '$file'"
      source "$file"
    else
      warn "file not found, unable to source: '$file'"
    fi
  done
}

benchmark_last() {
  local last="$(fc -ln -1)"
  hyperfine $@ "$last"
}

is_git() {
  local pwd="$1"
  [[ $(git -C "$pwd" rev-parse --is-inside-work-tree 2>/dev/null) == true ]]
}

git_info() {
  local pwd="$1"
  if ! is_git "$pwd"; then
    return
  fi
  alias _git="git -C $pwd"
  local changes branch remote remote_hash local_hash pushpull

  changes="$([[ -n $(_git status --porcelain) ]] && print -nP "$SYMBOL_CHANGES")"
  branch="$(_git symbolic-ref --short HEAD 2>/dev/null)"
  if [[ -z $branch ]]; then
    branch="$(_git branch | awk -F'[ ()]' '/HEAD detached at/ {print $3,$4,$5,$6}')"
  else
    remote="$(_git rev-parse --abbrev-ref --symbolic-full-name @{u})"
    remote="${remote%%/*}"
    remote_hash=($(_git ls-remote --head --exit-code "$remote"))
    remote_hash="${remote_hash[1]}"
    local_hash="$(_git rev-parse "$branch")"
    if [[ $local_hash != $remote_hash ]]; then
      if git merge-base --is-ancestor "$remote_hash" "$local_hash"; then
        pushpull="$SYMBOL_PUSH"
      elif git merge-base --is-ancestor "$local_hash" "$remote_hash"; then
        pushpull="$pushpull$SYMBOL_PULL"
      else
        # Often it won't be possible to tell if "pull" is truly the right thing to depict,
        # in those cases it's easier to just depict "out of sync."
        pushpull="$SYMBOL_OUTOFSYNC"
      fi
    fi
  fi
  unalias _git
  print -n "$(faintacc $branch) $changes${pushpull+"$pushpull"}"
}

go_version() {
  command_exists go || return
  go version | awk -F'go| ' '/go version go/ {print $5}'
}

versions() {
  print -n "Go $(go_version)"
}

laststatus() {
  local s=$1
  if (( s == 0 )); then
    pcolor "$s" green none bold
  else
    pcolor "$s" red none bold
  fi
}

aliases() {
  alias ls='ls -C --color'
  alias l='ls -lAh --color'
  alias tree='tree -lahC'
  alias gco='git checkout'
  alias gb='git branch'
  alias gr='git remote'
  alias gl='git pull'
  alias gcmsg='git commit -m'
  alias gca='git commit --amend'
}

asdf() {
  if [[ $(uname) == Darwin ]]; then
    source_if_exists "$HOMEBREW_PREFIX"/Cellar/asdf/*/libexec/asdf.sh
  else
    # TODO: asdf for Linux
  fi
}

custom_plugins() {
  # Source everything under zsh config dir
  zshrcd="$HOME/.config/zsh"
  [[ -d $zshrcd ]] && for f in $zshrcd/*; do source "$f"; done
}

fuzzy_finder() {
  command_exists fzf || warn "fuzzy finder setup requires fzf be installed: https://github.com/junegunn/fzf"
  command_exists rg || warn "fuzzy finder setup requires ripgrep be installed: https://github.com/BurntSushi/ripgrep"

  export FZF_TMUX_OPTS="-d"
  export FZF_ALT_C_OPTS="--preview 'tree -C {} | head -200'"
  export FZF_CTRL_T_COMMAND='rg --files --smart-case --glob "!Applications/" --glob "!Applications (Parallels)" --glob "!Library"'
  export FZF_DEFAULT_COMMAND='rg --files --hidden --smart-case --glob "!.git/*"'
  export FZF_DEFAULT_OPTS='--height 40% --reverse --border --extended'

  # Defaults generated by fzf
  #export FZF_CTRL_T_COMMAND='rg --files --hidden --glob "!.git/*"'
  #export FZF_DEFAULT_COMMAND='rg --files --hidden --glob "!.git/*" --smart-case'
  #export FZF_DEFAULT_OPTS='--height 40% --reverse --border --extended'

  command_exists fzf && source_if_exists ~/.fzf.zsh
}

homebrew() {
  [[ -f $HOMEBREW_PREFIX/bin/brew ]] && eval "$($HOMEBREW_PREFIX/bin/brew shellenv)"
}

zsh_settings() {
  unsetopt auto_cd
  # Save history shared but only when exiting session
  #setopt noincappendhistory
  setopt inc_append_history
  setopt nosharehistory
  setopt PROMPT_SUBST
  setopt autopushd

  autoload -U history-search-end
  zle -N history-beginning-search-backward-end history-search-end
  zle -N history-beginning-search-forward-end history-search-end
  bindkey '^[[A' history-beginning-search-backward-end
  bindkey '^[[B' history-beginning-search-forward-end

  PROMPT=$'\n$(faint %~) $PROMPT_GIT_INFO\n%(!.#.\$) '
  RPROMPT='$(laststatus $?)$(last_command_elapsed)'
}

nicetime() {
  local milliseconds=$1
  if (( milliseconds == 0 )); then return; fi
  local seconds=$(( milliseconds/1000 ))
  local minutes=$(( milliseconds/60000 ))
  local hours=$(( milliseconds/3600000 ))
  local days=$(( milliseconds/86400000 ))
  local hh=$(( hours-days*24 ))
  local mm=$(( minutes - hh*60 - days*24*60))
  local ss=$(( seconds - mm*60 - hh*60*60 - days*24*60*60 ))
  local mss=$(( milliseconds - ss*1000 - mm*60*1000 - hh*60*60*1000 - days*24*60*60*1000 ))
  local result
  if (( days > 0 )); then
    result+="${days}d "
  fi
  if (( hh > 0 )); then
    result+="${hh}h "
  fi
  if (( mm > 0 )); then
    result+="${mm}m "
  fi
  if (( ss > 0 )); then
    result+="${ss}s "
  fi
  if (( mss > 0 )); then
    result+="${mss}ms"
  fi
  print -n "$result"
}

last_command_elapsed() {
  if [[ ! $TIMER_ELAPSED ]]; then return; fi
  local nt="$(nicetime $TIMER_ELAPSED)"
  if [[ -n $nt ]]; then
    print -n " took $nt"
  fi
}

worker_start() {
  async_start_worker prompt_worker
  async_register_callback prompt_worker prompt_callback
}

prompt_callback() {
  PROMPT_GIT_INFO="$3"
  if [[ $job == '[async]' ]]; then
    if (( return_code == 2 )); then
        # Need to restart the worker. Stolen from
        # https://github.com/mengelbrecht/slimline/blob/master/lib/async.zsh
        worker_start
    fi
    PROMPT_GIT_INFO="ERR GETTING GIT STATUS: $return_code"
  fi
  # Reset the prompt, but only if there are no other async results in the buffer.
  # If there are, delay update until they've completed, so as to avoid strange states in ZLE.
  if (( $6 == 0 )); then
    zle reset-prompt
  fi
}

SYMBOL_PUSH="$(acc ⭡)"
SYMBOL_PULL="$(acc2 ⭣)"
SYMBOL_CHANGES="$(cau °)"
SYMBOL_SEPARATOR=_
SYMBOL_OUTOFSYNC="$(cau ⮃)"

git_branchinfo_fast() {
  # TODO: dedupe code with above git_info
  git -C "$1" symbolic-ref --short HEAD 2>/dev/null || git -C "$1" branch | awk -F'[ ()]' '/HEAD detached at/ {print $3,$4,$5,$6}'
}

worker_start

add-zsh-hook preexec() {
  timer=$(($(print -P %D{%s%6.})/1000))
}

add-zsh-hook precmd (){
  PROMPT_DATETIME="$(date +'%Y-%m-%d %H:%M:%S')"
  async_job prompt_worker git_info "$PWD"
  if [[ $timer ]]; then
    now=$(($(print -P %D{%s%6.})/1000))
    TIMER_ELAPSED=$((now-timer))
    unset timer
  fi
}

add-zsh-hook chpwd (){
  if is_git "$PWD"; then
    PROMPT_GIT_INFO="$(faintacc $(git_branchinfo_fast "$PWD"))"
  else
    PROMPT_GIT_INFO=""
  fi
  async_job prompt_worker git_info "$PWD"
}

homebrew
aliases
asdf
custom_plugins
fuzzy_finder
zsh_settings

# These functions helped with organization in this file and are unneeded elsewhere
unset -f aliases
unset -f asdf
unset -f custom_plugins
unset -f fuzzy_finder
unset -f homebrew
unset -f zsh_settings

