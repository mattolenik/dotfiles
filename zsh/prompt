# vi: ft=zsh

autoload -U add-zsh-hook

PROMPT_WORKER=prompt_worker 

_restart_worker() {
  async_stop_worker $PROMPT_WORKER
  async_start_worker $PROMPT_WORKER
  async_register_callback $PROMPT_WORKER _prompt_callback
}

_start_worker() {
  async_start_worker $PROMPT_WORKER
  async_register_callback $PROMPT_WORKER _prompt_callback
}

_prompt_git_info() {
  local pwd="$1"
  if ! is_git "$pwd"; then
    return
  fi
  alias _git="git -C $pwd"
  local changes branch remote remote_hash local_hash pushpull

  changes="$([[ -n $(_git status --porcelain) ]] && print -nP "$SYMBOL_CHANGES")"
  branch="$(_git symbolic-ref --short HEAD 2>/dev/null)"
  if [[ -z $branch ]]; then
    branch="$(_git branch | awk -F'[ ()]' '/HEAD detached at/ {print $3,$4,$5,$6}')"
  else
    remote="$(_git rev-parse --abbrev-ref --symbolic-full-name @{u})"
    remote="${remote%%/*}"
    remote_hash=($(_git ls-remote --head --exit-code "$remote"))
    remote_hash="${remote_hash[1]}"
    local_hash="$(_git rev-parse "$branch")"
    if [[ $local_hash != $remote_hash ]]; then
      if git merge-base --is-ancestor "$remote_hash" "$local_hash"; then
        pushpull="$SYMBOL_PUSH"
      elif git merge-base --is-ancestor "$local_hash" "$remote_hash"; then
        pushpull="$pushpull$SYMBOL_PULL"
      else
        # Often it won't be possible to tell if "pull" is truly the right thing to depict,
        # in those cases it's easier to just depict "out of sync."
        pushpull="$SYMBOL_OUTOFSYNC"
      fi
    fi
  fi
  unalias _git
  print -n "$(faintacc $branch) $changes${pushpull+"$pushpull"}"
}

_prompt_last_status() {
  local s=$1
  if (( s == 0 )); then
    pcolor "$s" green none bold
  else
    pcolor "$s" red none bold
  fi
}

_prompt_parts() {
  _prompt_vcs_info
}

_rprompt_parts() {
  _prompt_last_command_elapsed
}

_prompt_vcs_info() {
  print "$PROMPT_GIT_INFO"
}

_prompt_last_command_elapsed() {
  if [[ ! $TIMER_ELAPSED ]]; then return; fi
  local nt="$(nicetime $TIMER_ELAPSED)"
  if [[ -n $nt ]]; then
    print -n " took $nt"
  fi
}

_prompt_callback() {
### From https://github.com/mafredri/zsh-async
# 
# Get results from finished jobs and pass it to the to callback function. This is the only way to reliably return the job name,
# return code, output and execution time and with minimal effort.
# 
# If the async process buffer becomes corrupt, the callback will be invoked with the first argument being [async] (job name),
# non-zero return code and fifth argument describing the error (stderr).
# 
# The callback_function is called with the following parameters:
#
#    $1 job name, e.g. the function passed to async_job
#    $2 return code
#        Returns -1 if return code is missing, this should never happen, if it does, you have likely run into a bug.
#    $3 resulting (stdout) output from job execution
#    $4 execution time, floating point e.g. 0.0076138973 seconds
#    $5 resulting (stderr) error output from job execution
#    $6 has next result in buffer (0 = buffer empty, 1 = yes)
#        This means another async job has completed and is pending in the buffer, it's very likely that your callback function will be called a second time (or more) in this execution.
#        It's generally a good idea to e.g. delay prompt updates (zle reset-prompt) until the buffer is empty to prevent strange states in ZLE.
#
# Possible error return codes for the job name [async]:
#
#    1   Corrupt worker output.
#    2   ZLE watcher detected an error on the worker fd.
#    3   Response from async_job when worker is missing.
#    130 Async worker crashed, this should not happen but it can mean the file descriptor has become corrupt.
#        This must be followed by a async_stop_worker [name] and then the worker and tasks should be restarted.
#        It is unknown why this happens.
###
  if [[ $1 == '[async]' ]]; then
    if (( $2 == 130 )); then
      error "zsh-async fatal error $2 'async worker crashed', restarting"
      print "Task stdout: $3\nTask stderr: $5"
      _restart_worker
      return
    fi
    if (( $2 > 0 )); then
      error "Async prompt update failed, exit code $2"
      print "Task stdout: $3\nTask stderr: $5"
      return
    fi
  fi

  OLD_PROMPT_GIT_INFO="$PROMPT_GIT_INFO"
  PROMPT_GIT_INFO=$3
  if [[ $OLD_PROMPT_INFO != $PROMPT_GIT_INFO ]] && (( $6 == 0 )); then
    # Reset prompt but only if there are no other prompt callbacks going on
    zle reset-prompt
    if [[ -z "$ZPROMPT_DISABLE_ASYNC_BACKGROUND" ]]; then
      _restart_worker
      async_job $PROMPT_WORKER _prompt_git_info "$PWD"
    fi
  fi
}

SYMBOL_PUSH="$(acc ⭡)"
SYMBOL_PULL="$(acc2 ⭣)"
SYMBOL_CHANGES="$(cau °)"
SYMBOL_OUTOFSYNC="$(cau ⮃)"
OLD_PROMPT_GIT_INFO=
PROMPT_GIT_INFO=

_start_worker

add-zsh-hook preexec() {
  timer=$(($(print -P %D{%s%6.})/1000))
}

add-zsh-hook precmd () {
  PROMPT_DATETIME="$(date +'%Y-%m-%d %H:%M:%S')"
  _restart_worker
  async_job $PROMPT_WORKER _prompt_git_info "$PWD"
  if [[ $timer ]]; then
    now=$(($(print -P %D{%s%6.})/1000))
    TIMER_ELAPSED=$((now-timer))
    unset timer
  fi
}

add-zsh-hook chpwd (){
  if [[ -z ${ZPROMPT_DISABLE_PROJECT_FUNCTION:-} ]] && [[ -f "$PWD/.zsh/functions" ]]; then
    source "$PWD/.zsh/functions"
  fi
  if is_git "$PWD"; then
    PROMPT_GIT_INFO="$(faintacc $(git_branchinfo_fast "$PWD"))"
  else
    PROMPT_GIT_INFO=""
  fi
  _restart_worker
  async_job $PROMPT_WORKER _prompt_git_info "$PWD"
}


PROMPT=$'\n$(faint %~) $(_prompt_parts)\n%(!.#.\$) '
RPROMPT='$(_prompt_last_status $?)$(_rprompt_parts)'

